apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName "${rootProject.versionName}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = false
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        ViewPagerAnimatorLibrary(MavenPublication) {
            groupId 'com.stylingandroid.viewpageranimator'
            artifactId 'viewpageranimator'
            version versionName
            artifact("$buildDir/outputs/aar/viewpageranimator-release.aar")
            artifact sourceJar
            artifact javadocJar
        }
    }
}

dependencies {
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:${rootProject.supportLibrary}"
    testCompile 'junit:junit:4.12'
    testCompile "org.assertj:assertj-core:${rootProject.assertj}"
    testCompile 'org.mockito:mockito-core:2.7.22'
}

apply from: '../config/bintray.gradle'
apply from: '../config/static_analysis.gradle'

